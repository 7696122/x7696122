+++
title = "cypress"
author = [7696122]
lastmod = 2022-01-19T10:33:00+09:00
draft = false
+++

## cypress {#cypress}

nix로 cypress 설치 [nixpkgs/default.nix at nixos-unstable · NixOS/nixpkgs · GitHub](https://github.com/NixOS/nixpkgs/blob/nixos-unstable/pkgs/development/web/cypress/default.nix#L42)  

npm cypress 사용안하게 설정 [how to use NixOS's Cypress instead of npm's Cypress · GitHub](https://gist.github.com/r-k-b/2485f977b476aa3f76a47329ce7f9ad4#file-shell-nix)  


## cypress-overlay.nix {#cypress-overlay-dot-nix}

```nix
# When Cypress starts, it copies some files into `~/.config/Cypress/cy/production/browsers/chrome-stable/interactive/CypressExtension/`
# from the Nix Store, one of which it attempts to modify immediately after.
# As-is, this fails because the copied file keeps the read-only flag it had in
# the Store.
# Luckily, the code responsible is a plain text script that we can easily patch:
final: prev: {
  cypress = prev.cypress.overrideAttrs (oldAttrs: {
    installPhase = let
      old = "copyExtension(pathToExtension, extensionDest)";
      # This has only been tested against Cypress 6.0.0!
      newForChrome =
        "copyExtension(pathToExtension, extensionDest).then(() => fs_1.default.chmodAsync(extensionBg, 0o0644))";
      newForFirefox =
        "copyExtension(pathToExtension, extensionDest).then(() => fs.chmodAsync(extensionBg, 0o0644))";
    in ''
      sed -i 's/${old}/${newForChrome}/' \
          ./resources/app/packages/server/lib/browsers/chrome.js

      sed -i 's/${old}/${newForFirefox}/' \
          ./resources/app/packages/server/lib/browsers/utils.js
    '' + oldAttrs.installPhase;
  });
}
```


## shell.nix {#shell-dot-nix}

```nix
let pkgs = import <nixpkgs> { overlays = [ (import ./cypress-overlay.nix) ]; };
in pkgs.mkShell {
  name = "cypress-example";

  buildInputs = with pkgs; [
    cypress
    # (with dotnetCorePackages; combinePackages [ sdk_5_0 net_5_0 ])
    nodejs
  ];

  shellHook = ''
    export CYPRESS_INSTALL_BINARY=0
    export CYPRESS_RUN_BINARY=${pkgs.cypress}/bin/Cypress
  '';
}
```
